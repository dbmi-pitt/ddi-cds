// Immunosuppressants - Azole Antifungals DDI Alerting Rule
// Written By: Sam Rosko
// Last Update: 2017-01-16
// Status: Completed
// Expected Output: 3 patients, 2 fluconazole patients, 3 alerts

package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.Calendar;
import java.lang.String;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugStrength;
import edu.pitt.dbmi.ohdsiv5.db.ExtendedDrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.Measurement;
import edu.pitt.dbmi.ohdsiv5.db.Person;


/////////// 
// declare any global variables here
/////////// 
global org.hibernate.Session hibernateSession;
global java.lang.String currentDateStr;
global java.util.Calendar currentDate;
global java.util.Calendar within48hours;
global java.util.Calendar within28days;


/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 

// The first rule is a basic check to identify all patients on any dosage or form of an immunosuppressant and an azole at the same time
rule "IMMUNOSUPPRESSANTS - AZOLES -- NO FILTER"
    when
      // First, we look for patients exposed to an immunosuppressant on the ingredient level
      $clinDrugConcept1 : ConceptSetItem(csName == "Immunosuppressants Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId()) 
      // Second, we similarly look for exposure to an azole antifungal on the ingredient level
      $clinDrugConcept2 : ConceptSetItem(csName == "Azole Antifungals Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())
      // Next, we identify the specific drug exposures for both the immunosuppressant and the azole antifungal
      $clinDrugConcept3 : ConceptSetItem(csName == "Immunosuppressants") // concept set excludes opthalmic and inhaled epinephrine
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
	&& drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept4 : ConceptSetItem(csName == "Azole Antifungals")
      $dexp2 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de2.getDrugEraStartDate() 
	&& drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept2.getConceptId())
      // This creates the patient as an object that can be used in the RHS state in the right hand side of the rule
      $person : Person(personId == $de1.getPersonId())
    then	
      // This creates a new "state" for any patients who are found to be on both drugs...
      // This state can be used as an input on further rules to quickly filter to only those patients who have made it through previous rules
      RHSState rhsCur = new RHSState("basic concomitant exposure of an immunosuppressant and an azole antifungal", "yes", $person);
      insertLogical(rhsCur);
      // The output string reports simply that a potential interaction was identified through concurrent drug exposures and lists the drug IDs
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Immunosuppressant: %s; Azole Antifungal: %s.", 
          $de1.getPersonId(), 
          $clinDrugConcept3.getConceptId(), 
          $clinDrugConcept4.getConceptId());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tImmunosuppressant - Azole antifungal interaction\tbasic concomitant exposure", currentDateStr, $person.getPersonId());
      System.out.println(s);      
end

// The second rule takes the patients from the first rule and identifies those that are specifically involved in a fluconazole/immunosuppressant interaction
rule "IMMUNOSUPPRESSANTS - AZOLES -- Fluconazole"
    when
      // This checks the state of the patients to identify only those who were identified to be on both medications of interest
      $rhs : RHSState(stateName == "basic concomitant exposure of an immunosuppressant and an azole antifungal" && state == "yes", $person : person)
      // Next, we need to insure that the patient was exposed to fluconazole on the ingredient level
      $clinDrugConcept1 : ConceptSetItem(csName == "Fluconazoles Ingredients")
      $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId()) 
      // And on the clinical drug level
      $clinDrugConcept2 : ConceptSetItem(csName == "Fluconazoles")
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
	&& drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
    then	
      // This RHS state shows that a patient was exposed to immunosuppressants and specifically fluconazole, and it will be used by the next two rules
      RHSState rhsCur = new RHSState("immunosuppressant and fluconazole", "yes", $person);
      insertLogical(rhsCur);
      // Nothing hugely important to output here, just an update to show how many patients were on fluconazole rather than other azoles
      String s = String.format(
          "Matched drug exposures for patient at the clinical drug level: Patient ID: %s; Fluconazole: %s.", // didnt include the immunosuppressant here cause it felt unnecessary
          $de1.getPersonId(),  
          $clinDrugConcept2.getConceptId());
      System.out.println(s);
end

// The third rule takes all patients on fluconazole and filters it down to only those on oral fluconazole
// In addition, it checks whether or not these patients are taking over 100 mg/day of oral fluconazole
// If so, an alert is fired
rule "IMMUNOSUPPRESSANTS - AZOLES -- Fluconazole PO"
    when
	// This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs : RHSState(stateName == "basic concomitant exposure of an immunosuppressant and an azole antifungal" && state == "yes", $person : person)	
	// Next, ensure that the azole is fluconazole
	$rhs2 : RHSState(stateName == "immunosuppressant and fluconazole" && state == "yes" && person.getPersonId() == $person.getPersonId())
        // Next, we pull in the drug era for fluconazole (ingredient level)
        $clinDrugConcept1 : ConceptSetItem(csName == "Fluconazoles Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // From there, we can pull in the specific oral fluconazole drug exposure, and check it against our 100 mg/day threshold
        $clinDrugConcept2 : ConceptSetItem(csName == "Fluconazoles PO")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() 
	  && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 100)      
    then
	// This is a placeholder RHS state that is currently not used any further in the rule
        RHSState rhsNN = new RHSState("immunosuppressant and high dose fluconazole", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
	// This outputs the recommendations and rationale for the patient because this is a leaf node  
        String s = String.format(
          "Immunosuppressant - Azole interaction for patient: %s.\n\tClinical implication: Risk of immunosuppressant toxicity.\n\tMitigating factor: Presence of an immunosuppressant and a high dose of oral fluconazole (Daily Dosage: %s mg/day, compared to the threshold value of 100 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Azole antifungals inhibit cytochrome P450 (CYP450) enzymes that metabolize immunosuppressants, leading to a potential risk of toxicity.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
      s = String.format("DATA\t%s\t%s\tImmunosuppressant - Azole antifungal interaction\timmunosuppressant and high dose oral fluconazole", currentDateStr, $person.getPersonId());
      System.out.println(s);      	
end

// The fourth rule takes all patients on fluconazole and filters it down to only those on intravenous fluconazole
// In addition, it checks whether or not these patients are taking over 200 mg/day of intravenous fluconazole
// If so, an alert is fired
rule "IMMUNOSUPPRESSANTS - AZOLES -- Fluconazole IV"
    when
	// This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs : RHSState(stateName == "basic concomitant exposure of an immunosuppressant and an azole antifungal" && state == "yes", $person : person)	
	// Next, ensure that the azole is fluconazole
	$rhs2 : RHSState(stateName == "immunosuppressant and fluconazole" && state == "yes" && person.getPersonId() == $person.getPersonId())
        // Next, we pull in the drug era for fluconazole (ingredient level)
        $clinDrugConcept1 : ConceptSetItem(csName == "Fluconazoles Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())        
        // From there, we can pull in the specific intravenous fluconazole drug exposure, and check it against our 200 mg/day threshold
        $clinDrugConcept2 : ConceptSetItem(csName == "Fluconazoles IV")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() 
	  && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 200)      
    then	
	// This is a placeholder RHS state that is currently not used any further in the rule
        RHSState rhsNN = new RHSState("immunosuppressant and high dose fluconazole", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
	// This outputs the recommendations and rationale for the patient because this is a leaf node  
        String s = String.format(
          "Immunosuppressant - Azole interaction for patient: %s.\n\tClinical implication: Risk of immunosuppressant toxicity.\n\tMitigating factor: Presence of an immunosuppressant and a high dose of intravenous fluconazole (Daily Dosage: %s mg/day, compared to the threshold value of 200 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Azole antifungals inhibit cytochrome P450 (CYP450) enzymes that metabolize immunosuppressants, leading to a potential risk of toxicity.", 
          $de1.getPersonId(), 
          $dexp1.getDailyDosage());
        System.out.println(s);
      s = String.format("DATA\t%s\t%s\tImmunosuppressant - Azole antifungal interaction\timmunosuppressant and a high dose of intravenous fluconazole", currentDateStr, $person.getPersonId());
      System.out.println(s);      		
end

// The fifth and final rule takes the patients from the first rule and identifies those that are NOT involved in a fluconazole/immunosuppressant interaction
rule "IMMUNOSUPPRESSANTS - AZOLES -- No Fluconazole"
    when
      // This checks the state of the patients to identify only those who were identified to be on both medications of interest
      $rhs : RHSState(stateName == "basic concomitant exposure of an immunosuppressant and an azole antifungal" && state == "yes", $person : person)
      // Next, ensure that the azole is not fluconazole
      not ($rhs2 : RHSState(stateName == "immunosuppressant and fluconazole" && state == "yes" && person.getPersonId() == $person.getPersonId()))            
      // Next we check the specific azole antifungal ingredient the patient was exposed to
      $clinDrugConcept1 : ConceptSetItem(csName == "Azole Antifungals Ingredients")
      $de1 : DrugEra(PersonId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())      
       // Followed by the specific clinical drug exposure (this is done to be able to output the concept ID, not necessary for rule itself)
      $clinDrugConcept2 : ConceptSetItem(csName == "Azole Antifungals")
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
      && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())     

    then	
    	// This is a placeholder RHS state that is currently not used any further in the rule
        RHSState rhsNN = new RHSState("immunosuppressant and non-fluconazole antifungal", "yes", $rhs.getPerson());
        insertLogical(rhsNN);
	// This outputs the recommendations and rationale for the patient because this is a leaf node  
        String s = String.format(
          "Immunosuppressant - Azole interaction for patient: %s.\n\tClinical implication: Risk of immunosuppressant toxicity.\n\tMitigating factor: Presence of an immunosuppressant and a non-fluconazole azole (%s).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Azole antifungals inhibit cytochrome P450 (CYP450) enzymes that metabolize immunosuppressants, leading to a potential risk of toxicity.", 
          $de1.getPersonId(), 
          $dexp1.getDrugConceptId());
        System.out.println(s);
      s = String.format("DATA\t%s\t%s\tImmunosuppressant - Azole antifungal interaction\timmunosuppressant and non-fluconazole antifungal", currentDateStr, $person.getPersonId());
      System.out.println(s);      		
end