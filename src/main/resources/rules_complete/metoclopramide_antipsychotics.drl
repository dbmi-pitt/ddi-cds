// Metoclopramide - Antipsychotics DDI Alerting Rule
// Written By: Sam Rosko
// Last Update: 2017-01-17
// Status: Completed, though the explanation needs to be justified
// Expected Output: 3 alerts (no filtering)

package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.Calendar;
import java.lang.String;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugStrength;
import edu.pitt.dbmi.ohdsiv5.db.ExtendedDrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.Measurement;
import edu.pitt.dbmi.ohdsiv5.db.Person;
import edu.pitt.dbmi.ohdsiv5.db.VisitOccurrence;


/////////// 
// declare any global variables here
/////////// 
global org.hibernate.Session hibernateSession;
global java.lang.String currentDateStr;
global java.util.Calendar currentDate;
global java.util.Calendar within48hours;
global java.util.Calendar within28days;


/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 

// The first rule is a basic check to identify all patients on any dosage or form of metoclopramide and an antipsychotic at the same time
rule "METOCLOPRAMIDE - ANTIPSYCHOTICS -- NO FILTER"
    when
      // First, we look for patients exposed to metoclopramide on the ingredient level
      $clinDrugConcept1 : ConceptSetItem(csName == "Metoclopramides Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId())       
      // Second, we similarly look for exposure to an antipsychotic on the ingredient level
      $clinDrugConcept2 : ConceptSetItem(csName == "Antipsychotics Ingredients" || csName == "Cholinesterase Inhibitors Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())
      // Next, we identify the specific drug exposures for both metoclopramide and the antipsychotic
      $clinDrugConcept3 : ConceptSetItem(csName == "Metoclopramides")
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
      && drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept4 : ConceptSetItem(csName == "Antipsychotics" || csName == "Cholinesterase Inhibitors")
      $dexp2 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de2.getDrugEraStartDate() 
      && drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept2.getConceptId())
      // This creates the patient as an object that can be used in the RHS state in the right hand side of the rule
      $person : Person(personId == $de1.getPersonId())
    then
      // This creates a new "state" for any patients who are found to be on both drugs...
      // This state can be used as an input on further rules to quickly filter to only those patients who have made it through previous rules
      RHSState rhsCur = new RHSState("concomitant exposure of metoclopramide and an antipsychotic or cholinesterase inhibitor", "yes", $person);
      insertLogical(rhsCur);
      // The output string reports simply that a potential interaction was identified through concurrent drug exposures and lists the drug IDs
      String s = String.format(
        "Metoclopramide - Antipsychotics interaction for patient: %s.\n\tClinical implication: Possible increased risk of tardive dyskinesia\n\tMitigating factor: Co-prescription of metoclopramide and an antipsychotic or a cholinesterase inhibitor (%s).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: Metoclopramide, antipsychotics, and cholinesterase inhibitors are all known in increase risk of tardive dyskinesia.", 
        $de1.getPersonId(), 
        $dexp2.getDrugConceptId());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tMetoclopramide and an Antipsychotic or Cholinesterase inhibitor\tbasic concomitant exposure", currentDateStr, $person.getPersonId());
      System.out.println(s);      
end