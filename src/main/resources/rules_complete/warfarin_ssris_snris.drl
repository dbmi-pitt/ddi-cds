// Warfarin - SSRIs/SNRIs DDI Alerting Rule
// Written By: Sam Rosko
// Last Update: 2017-01-16
// Status: Completed
// Expected Output: 5, 1 age, 2 another drug, 1 condition, 5 alerts

package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.Calendar;
import java.lang.String;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugStrength;
import edu.pitt.dbmi.ohdsiv5.db.ExtendedDrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.Measurement;
import edu.pitt.dbmi.ohdsiv5.db.Person;


/////////// 
// declare any global variables here
/////////// 
global org.hibernate.Session hibernateSession;
global java.lang.String currentDateStr;
global java.util.Calendar currentDate;
global java.util.Calendar within48hours;
global java.util.Calendar within28days;


/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 

// The first rule is a basic check to identify all patients on any dosage or form of warfarin and an SSRI/SNRI at the same time
rule "WARFARIN - SSRI+SNRI -- NO FILTER"
    when
      // First, we look for patients exposed to warfarin on the ingredient level
      $clinDrugConcept1 : ConceptSetItem(csName == "Warfarins Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId()) 
      // Second, we similarly look for exposure to any SSRIs/SNRIs on the ingredient level
      $clinDrugConcept2 : ConceptSetItem(csName == "SSRIs and SNRIs Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())
      // Next, we identify the specific drug exposures for both warfarin and the SSRI/SNRI
      $clinDrugConcept3 : ConceptSetItem(csName == "Warfarins")
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept4 : ConceptSetItem(csName == "SSRIs and SNRIs")
      $dexp2 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de2.getDrugEraStartDate() && drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept2.getConceptId())
      // This creates the patient as an object that can be used in the RHS state in the right hand side of the rule
      $person : Person(personId == $de1.getPersonId())
    then	
      // This creates a new "state" for any patients who are found to be on both drugs...
      // This state can be used as an input on further rules to quickly filter to only those patients who have made it through previous rules
      RHSState rhsCur = new RHSState("basic concomitant exposure of warfarin and SSRI/SNRI", "yes", $person);
      insertLogical(rhsCur);
      // The output string reports simply that a potential interaction was identified through concurrent drug exposures and lists the drug IDs    
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Anticoagulant: %s; SSRI/SNRI: %s.", 
          $de1.getPersonId(), 
          $clinDrugConcept3.getConceptId(), 
          $clinDrugConcept4.getConceptId());
      System.out.println(s);
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - SSRI/SNRI interaction\tbasic concomitant exposure", currentDateStr, $person.getPersonId());
      System.out.println(s);      
end

// The second rule is an intermediate node that checks exposed patients for a specific risk factor...
// In this case, the risk factor is the age of the patient... A patient is at risk if their age is > 65 years
rule "WARFARIN - SSRI+SNRI -- AGE"
    when
	// This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and SSRI/SNRI" && state == "yes", $person : person)
	// This checks the patient from the previous RHS state to see if they are >65 years of age
        Person(personId == $person.getPersonId() && yearOfBirth <= 1951)
    then
	// This tags a given patient as "yes for risks"... There are three potential types of risks but all of them result in this same RHS state
        RHSState rhsNN = new RHSState("warfarin and SSRI/SNRI exposure - yes risk", "yes", $person);
        insertLogical(rhsNN);
        // Nothing hugely important to output here, just an update to show how many patients were at risk due to age
        String s = String.format(
            "Matched drug exposures for the patient with AGE as a risk factor: Patient ID: %s; Age: %s, which exceeds the threshold value of 65.", 
            $person.getPersonId(),
	    (currentDate.get(Calendar.YEAR) - $person.getYearOfBirth()));
        System.out.println(s);
end

// The third rule is an intermediate node that checks exposed patients for a specific risk factor...
// In this case, the risk factor is whether or not the patient is on risk-enhancing drugs... A patient is at risk if they are on a systemic corticosteroid or an aldosterone antagonist
rule "WARFARIN - SSRI+SNRI -- OTHER DRUGS"
    when
	// This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and SSRI/SNRI" && state == "yes", $person : person)
	// This checks to see if the patient is on either group drug of interest at the ingredient level
	$clinDrugConcept1 : ConceptSetItem(csName == "Corticosteroids Ingredients" || csName == "Aldosterone Antagonists Ingredients")
        $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
        // This identifies the specific clinical drug that the patient was exposed to as a risk factor
        $clinDrugConcept2 : ConceptSetItem(csName == "Systemic Corticosteroids" || csName == "Aldosterone Antagonists")
        $dexp1 : ExtendedDrugExposure(personId == $person.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())        
    then
    	// This tags a given patient as "yes for risks"... There are three potential types of risks but all of them result in this same RHS state
        RHSState rhsNN = new RHSState("warfarin and SSRI/SNRI exposure - yes risk", "yes", $person);
        insertLogical(rhsNN);
        // Nothing hugely important to output here, just an update to show how many patients were at risk due to other drugs
        String s = String.format(
            "Matched drug exposures for the patient with ANOTHER DRUG as a risk factor: Patient ID: %s; Drug %s.", 
            $person.getPersonId(),
            $dexp1.getDrugConceptId());
        System.out.println(s);
end

// The fourth rule is an intermediate node that checks exposed patients for a specific risk factor...
// In this case, the risk factor is group of conditions... A patient is at risk if they have a history of GI bleeds or peptic ulcers
rule "WARFARIN - SSRI+SNRI -- CONDITIONS"
    when
	// This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and SSRI/SNRI" && state == "yes", $person : person)
	// This checks to see if the patient has any of the conditions of interest
	$condConcept1 : ConceptSetItem(csName == "History of GI Bleeds")	
        $ce1 : ConditionEra(PersonId == $person.getPersonId() && conditionConceptId == $condConcept1.getConceptId())
    then	
    	// This tags a given patient as "yes for risks"... There are three potential types of risks but all of them result in this same RHS state
        RHSState rhsNN = new RHSState("warfarin and SSRI/SNRI exposure - yes risk", "yes", $person);
        insertLogical(rhsNN);
        // Nothing hugely important to output here, just an update to show how many patients were at risk due to conditions
        String s = String.format(
            "Matched drug exposures for the patient with a CONDITION as a risk factor - Patient ID: %s; Condition: %s.", 
            $person.getPersonId(),
	    $ce1.getConditionConceptId());
        System.out.println(s);
end

// The fifth rule is one of the two leaf nodes
// In this rule, we check if patients had any risk factors as identified above, if they do, an alert is fired
rule "WARFARIN - SSRI+SNRI -- YES RISKS"
    when
	// This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and SSRI/SNRI" && state == "yes", $person : person)	
	// Next, we check if the patient was exposed to any other risk factors
	$rhs2 : RHSState(stateName == "warfarin and SSRI/SNRI exposure - yes risk" && state == "yes" && person.getPersonId() == $person.getPersonId())
    then	
        // This state is largely unused atm, as it is a leaf node (end of a branch)
        RHSState rhsNN = new RHSState("warfarin and SSRI/SNRI exposure - yes risks", "yes", $person);
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node    
        String s = String.format(
            "Warfarin - SSRI/SNRI interaction for patient: %s.\n\tClinical implication: Increased risk of UGIB or other bleeding.\n\tContextual factor: Presence of age, condition, or drug-factor risks. \n\tRecommendation: Use only if benefit outweighs risk.\n\tExplanation: If possible avoid drugs that increase bleeding risk (e.g., NSAIDs, aldosterone antagonists, aspirin, corticoids). \n\tTo reduce risk of GI bleeds consider adding a PPI and avoiding the SSRI/SNRI.", 
            $person.getPersonId());
        System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - SSRI/SNRI interaction\tyes risks", currentDateStr, $person.getPersonId());
      System.out.println(s);      	
end

// The sixth rule is the other of the two leaf nodes and the opposite of the fifth rule
rule "WARFARIN - SSRI+SNRI -- NO RISKS"
    when
	// This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and SSRI/SNRI" && state == "yes", $person : person)
	// Next, we make sure that the patient was not exposed to any other risk factors
        not (RHSState(stateName == "warfarin and SSRI/SNRI exposure - yes risk" && state == "yes" && person.getPersonId() == $person.getPersonId()))
    then	
        // This state is largely unused atm, as it is a leaf node (end of a branch)
	RHSState rhsNN = new RHSState("warfarin and SSRI/SNRI exposure - no risks", "yes", $person);
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node    
        String s = String.format(
            "Warfarin - SSRI/SNRI interaction for patient: %s.\n\tClinical implication: Increased risk of UGIB or other bleeding.\n\tContextual factor: No age, condition, or drug-factor risks.\n\tRecommendation: Use only if benefit outweighs risk.\n\tExplanation: To reduce the bleeding risk, consider avoiding the SSRI/SNRI.", 
            $person.getPersonId());
        System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - SSRI/SNRI interaction\tno risks", currentDateStr, $person.getPersonId());
      System.out.println(s);      		
end