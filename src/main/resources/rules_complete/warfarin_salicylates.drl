// Warfarin - Salicylates DDI Alerting Rule
// Written By: Sam Rosko
// Last Update: 2017-01-17
// Status: Completed
// Expected Output: 6 patients, 3 alerts

package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.List;
import java.util.ArrayList;
import java.util.Map;
import java.util.Calendar;
import java.lang.String;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugStrength;
import edu.pitt.dbmi.ohdsiv5.db.ExtendedDrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.Measurement;
import edu.pitt.dbmi.ohdsiv5.db.Person;
import edu.pitt.dbmi.ohdsiv5.db.VisitOccurrence;


/////////// 
// declare any global variables here
/////////// 
global org.hibernate.Session hibernateSession;
global java.lang.String currentDateStr;
global java.util.Calendar currentDate;
global java.util.Calendar within48hours;
global java.util.Calendar within28days;


/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 

// The first rule is a basic check to identify all patients on any dosage or form of warfarin and a salicylate at the same time
rule "WARFARIN - SALICYLATES -- NO FILTER"
    when
      // First, we look for patients exposed to warfarin on the ingredient level
      $clinDrugConcept1 : ConceptSetItem(csName == "Warfarins Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId())       
      // Second, we similarly look for exposure to a salicylate on the ingredient level
      $clinDrugConcept2 : ConceptSetItem(csName == "Salicylates Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())
      // Next, we identify the specific drug exposures for both warfarin and the salicylate
      $clinDrugConcept3 : ConceptSetItem(csName == "Warfarins")
      $dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
	&& drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept4 : ConceptSetItem(csName == "Salicylates")
      $dexp2 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de2.getDrugEraStartDate() 
	&& drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept2.getConceptId())
      // This creates the patient as an object that can be used in the RHS state in the right hand side of the rule
      $person : Person(personId == $de1.getPersonId())
    then
      // This creates a new "state" for any patients who are found to be on both drugs...
      // This state can be used as an input on further rules to quickly filter to only those patients who have made it through previous rules
      RHSState rhsCur = new RHSState("basic concomitant exposure of warfarin and a salicylate", "yes", $person);
      insertLogical(rhsCur);
      // The output string reports simply that a potential interaction was identified through concurrent drug exposures and lists the drug IDs
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Warfarin: %s; Salicylate: %s.", 
          $de1.getPersonId(), 
          $clinDrugConcept3.getConceptId(),
          $clinDrugConcept4.getConceptId());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate\tbasic concomitant exposure", currentDateStr, $person.getPersonId());
      System.out.println(s);      
end

// The second rule filters the patients found to be on both medications to those only on Aspirin
// From there, it checks the dosage of Aspirin and fires an alert if the patient has been given over 3 g/day
rule "WARFARIN - SALICYLATES -- ASPIRIN"
    when
	// This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and a salicylate" && state == "yes", $person : person)
	// Here, we check to see if the patient is specifically on the salicylate Aspirin
	// At the same time, we check if the patient is on a high dose of aspirin (3 g/day, or 3000 mg/day)
	// If they exceed this daily dosage, we fire an alert
        $clinDrugConcept1 : ConceptSetItem(csName == "Aspirins Ingredients")
	$de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
	$clinDrugConcept2 : ConceptSetItem(csName == "Aspirins")
	$dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
	  && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 3000)
    then
	// This tags a given patient as "yes for warfarin and aspirin", which is a state unlikely to be used in further rules
        RHSState rhsNN = new RHSState("warfarin and aspirin", "yes", $person);
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node
        String s = String.format(
          "Warfarin - Salicylate interaction for patient: %s.\n\tClinical implication: Increased risk of UGIB or other bleeding.\n\tMitigating factor: High dosage of Aspirin (%s mg/day, compared to a threshold value of 3000.0 mg/day).\n\tRecommendation: Use only if benefit outweighs risk.\n\tExplanation: If the aspirin is being used as an analgesic or antipyretic, it would be prudent to use an alternative such as acetaminophen. \n\tIn some people, acetaminophen can increase the anticoagulant effect of warfarin, so monitor the INR if acetaminophen is used in doses over \n\t2 g/day for a few days. If the aspirin is being used as an anti-inflammatory agent, also consider using acetaminophen. The use of NSAIDs \n\tsince NSAIDs increase the risk of GI hemorrhage in patients on warfarin. NSAIDs also have antiplatelet effects but unlike aspirin, the \n\tantiplatelet effect is not for the life of the platelet, but rather only as long as the NSAID is in the circulation.", 
          $person.getPersonId(),
	  $dexp1.getDailyDosage());
        System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate\twarfarin and aspirin", currentDateStr, $person.getPersonId());
      System.out.println(s);      	
end

// The third rule filters patients found to be on both medications to those taking a non-acetylated salicylate
// This does not include Bismuth subsalicylate, as it has a different dosage threshold than the rest
// In this case, we again check to see if the dosage of the salicylate is over 3g/day and fire an alert if so
rule "WARFARIN - SALICYLATES -- NON-ACETYLATED"
    when
	// This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and a salicylate" && state == "yes", $person : person)
	// Here, we check to see if the patient is specifically on a non-acetylated salicylate (not including bismuth subsalicylate)
	// At the same time, we check if the patient is on a high dose of these salicylates (equivalent to 3 g/day Aspirin, which is 3000 mg/day)
	// If they exceed this daily dosage, we fire an alert
        $clinDrugConcept1 : ConceptSetItem(csName == "Non-acetylated Salicylates Ingredients")
	$de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
	$clinDrugConcept2 : ConceptSetItem(csName == "Non-acetylated Salicylates")
	$dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
	  && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 3000)
    then
	// This tags a given patient as "yes for warfarin and a non-acetylated salicylate", which is a state unlikely to be used in further rules
        RHSState rhsNN = new RHSState("warfarin and non-acetylated salicylate", "yes", $person);
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node
        String s = String.format(
          "Warfarin - Salicylate interaction for patient: %s.\n\tClinical implication: Increased risk of UGIB or other bleeding.\n\tMitigating factor: High dosage of a non-acetylated salicylate (%s at %s mg/day, compared to a threshold value of 3000.0 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: While non-acetylated salicylates do not affect platelets and would not interact with warfarin on that basis, high serum concentrations of salicylate can increase the INR.", 
          $person.getPersonId(),
	  $dexp1.getDrugConceptId(),
	  $dexp1.getDailyDosage());
        System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate\twarfarin and non-acetylated salicylate", currentDateStr, $person.getPersonId());
      System.out.println(s);      		
end

// The fourth anf final rule filters patients found to be on both medication classes to those taking bismuth subsalicylate
// This is a non-acetylated salicylate, so we need to check if its dosage equivalent is equal to 3000 mg/day Aspirin
// Unlike the other salicylates, bismuth subsalicylate is does not have equivalent dosage to aspirin, therefore a conversion must be made
// Bismuth Salicylate is 2.57g / 1 g of Aspirin
rule "WARFARIN - SALICYLATES -- BISMUTH SUBSALICYLATE"
    when
	// This checks the state of the patients to identify only those who were identified to be on both medications of interest
        $rhs1 : RHSState(stateName == "basic concomitant exposure of warfarin and a salicylate" && state == "yes", $person : person)
	// Here, we check to see if the patient is specifically on the non-acetylated salicylate, bismuth subsalicylate
	// At the same time, we check if the patient is on a high dose of bismuth subsalicylate (equivalent to 3 g/day Aspirin, which is 7710 mg/day)
	// If they exceed this daily dosage, we fire an alert
        $clinDrugConcept1 : ConceptSetItem(csName == "Bismuth Subsalicylates Ingredients")
	$de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
	$clinDrugConcept2 : ConceptSetItem(csName == "Bismuth Subsalicylates")
	$dexp1 : ExtendedDrugExposure(personId == $de1.getPersonId() && drugExposureStartDate == $de1.getDrugEraStartDate() 
	  && drugConceptId == $clinDrugConcept2.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId() && dailyDosage > 7710)
    then
	// This tags a given patient as "yes for warfarin and bismuth subsalicylate", which is a state unlikely to be used in further rules
        RHSState rhsNN = new RHSState("warfarin and bismuth subsalicylate", "yes", $person);
        insertLogical(rhsNN);
        // This outputs the recommendations and rationale for the patient because this is a leaf node
        String s = String.format(
          "Warfarin - Salicylate interaction for patient: %s.\n\tClinical implication: Increased risk of UGIB or other bleeding.\n\tMitigating factor: High dosage of Bismuth Subsalicylate (%s mg/day, compared to a threshold value of 7710.0 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: While non-acetylated salicylates do not affect platelets and would not interact with warfarin on that basis, high serum concentrations of salicylate can increase the INR.", 
          $person.getPersonId(),
	  $dexp1.getDailyDosage());
        System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate\twarfarin and bismuth subsalicylate", currentDateStr, $person.getPersonId());
      System.out.println(s);      			
end