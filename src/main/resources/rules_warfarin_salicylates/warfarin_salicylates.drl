// warfarin - salicylates DDI alerting rule

package com.sample

//list any import classes here.
import function com.sample.DRLLogger.*;

import java.util.*;
import java.lang.String;
import java.sql.Timestamp;

import com.sample.model.ConceptSetItem;
import com.sample.model.RHSState;

import edu.pitt.dbmi.ohdsiv5.db.ConditionEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugEra;
import edu.pitt.dbmi.ohdsiv5.db.DrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.DrugStrength;
import edu.pitt.dbmi.ohdsiv5.db.ExtendedDrugExposure;
import edu.pitt.dbmi.ohdsiv5.db.Measurement;
import edu.pitt.dbmi.ohdsiv5.db.Person;


/////////// 
// declare any global variables here
/////////// 
global java.lang.String currentDateStr;
global java.sql.Timestamp currentDate;
global java.sql.Timestamp within48hours;
global java.sql.Timestamp within28days;
global java.sql.Timestamp plus1day;

/////////// 
// declare any types here
/////////// 


/////////// 
// Rules
/////////// 

// basic check of any dosage or form of warfarin and a salicylate at the same time
rule "WARFARIN - SALICYLATES -- NO FILTER"
    when
      // First, we look for patients exposed to warfarin on the ingredient level
      $clinDrugConcept1 : ConceptSetItem(csName == "Warfarins Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId()) 
      // Second, we similarly look for exposure to a salicylate on the ingredient level
      $clinDrugConcept2 : ConceptSetItem(csName == "Salicylates Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())
      // Next, we identify the specific drug exposures for both warfarin and the salicylate
      $clinDrugConcept3 : ConceptSetItem(csName == "Warfarins") 
      $clinDrugConcept4 : ConceptSetItem(csName == "Salicylates")
      $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
        && drugExposureStartDate != null && drugExposureStartDate >= currentDate && drugExposureStartDate < plus1day
        && drugConceptId == $clinDrugConcept3.getConceptId() && ingredientConceptId == $clinDrugConcept1.getConceptId())
      $dexp2 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
        && (
          (drugExposureStartDate >= $dexp1.getDrugExposureStartDate() && drugExposureStartDate <= $dexp1.getDrugExposureEndDate())
          || (drugExposureEndDate >= $dexp1.getDrugExposureStartDate() && drugExposureEndDate <= $dexp1.getDrugExposureEndDate())
          || ($dexp1.getDrugExposureStartDate() >= drugExposureStartDate && $dexp1.getDrugExposureStartDate() <= drugExposureEndDate)
          || ($dexp1.getDrugExposureEndDate() >= drugExposureStartDate && $dexp1.getDrugExposureEndDate() <= drugExposureEndDate)
        )
        && drugConceptId != null && drugConceptId == $clinDrugConcept4.getConceptId() 
        && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept2.getConceptId())
      // This creates the patient as an object that can be used in the RHS state in the right hand side of the rule
      $person : Person(personId == $de1.getPersonId())
    then
      // This creates a new "state" for any patients who are found to be on both drugs...
      // This state can be used as an input on further rules to quickly filter to only those patients who have made it through previous rules
      RHSState rhsCur = new RHSState("basic concomitant exposure of warfarin and a salicylate", "yes", $person, $dexp1, $dexp2);
      insertLogical(rhsCur);
      // The output string reports simply that a potential interaction was identified through concurrent drug exposures and lists the drug IDs
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Anticoagulant: %s; Salicylate: %s;", 
          $de1.getPersonId(), 
          $clinDrugConcept3.getConceptId(), 
          $clinDrugConcept4.getConceptId());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate interaction\tbasic concomitant exposure\t%s\t%s\t%s\t%s\t%s\t%s",
          currentDateStr,
          $person.getPersonId(),
          "false", "basic concomitant",
          $person.getGenderCUI(),
          ((double) ($person.getDobDateDiffInMillis(currentDate) / (1000 * 3600 * 24)) / 365),
          $dexp1.getLogInfo(),
          $dexp2.getLogInfo()
      );
      System.out.println(s);
end

rule "WARFARIN - SALICYLATES -- NO FILTER, ALT"
    when
      // First, we look for patients exposed to warfarin on the ingredient level
      $clinDrugConcept1 : ConceptSetItem(csName == "Warfarins Ingredients")
      $de1 : DrugEra(drugConceptId == $clinDrugConcept1.getConceptId()) 
      // Second, we similarly look for exposure to a salicylate on the ingredient level
      $clinDrugConcept2 : ConceptSetItem(csName == "Salicylates Ingredients")
      $de2 : DrugEra(PersonId == $de1.getPersonId() && drugConceptId == $clinDrugConcept2.getConceptId())
      // Next, we identify the specific drug exposures for both warfarin and the salicylate
      $clinDrugConcept3 : ConceptSetItem(csName == "Warfarins") 
      $clinDrugConcept4 : ConceptSetItem(csName == "Salicylates")
      $dexp2 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
        && drugExposureStartDate != null && drugExposureStartDate >= currentDate && drugExposureStartDate < plus1day
        && drugConceptId == $clinDrugConcept4.getConceptId() && ingredientConceptId == $clinDrugConcept2.getConceptId())
      $dexp1 : ExtendedDrugExposure(personId != null && personId == $de1.getPersonId()
        && (
          (drugExposureStartDate >= $dexp2.getDrugExposureStartDate() && drugExposureStartDate <= $dexp2.getDrugExposureEndDate())
          || (drugExposureEndDate >= $dexp2.getDrugExposureStartDate() && drugExposureEndDate <= $dexp2.getDrugExposureEndDate())
          || ($dexp2.getDrugExposureStartDate() >= drugExposureStartDate && $dexp2.getDrugExposureStartDate() <= drugExposureEndDate)
          || ($dexp2.getDrugExposureEndDate() >= drugExposureStartDate && $dexp2.getDrugExposureEndDate() <= drugExposureEndDate)
        )
        && drugConceptId != null && drugConceptId == $clinDrugConcept3.getConceptId() 
        && ingredientConceptId != null && ingredientConceptId == $clinDrugConcept1.getConceptId())
      // This creates the patient as an object that can be used in the RHS state in the right hand side of the rule
      $person : Person(personId == $de1.getPersonId())
    then
      // This creates a new "state" for any patients who are found to be on both drugs...
      // This state can be used as an input on further rules to quickly filter to only those patients who have made it through previous rules
      RHSState rhsCur = new RHSState("basic concomitant exposure of warfarin and a salicylate", "yes", $person, $dexp1, $dexp2);
      insertLogical(rhsCur);
      // The output string reports simply that a potential interaction was identified through concurrent drug exposures and lists the drug IDs
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Anticoagulant: %s; Salicylate: %s;", 
          $de1.getPersonId(), 
          $clinDrugConcept3.getConceptId(), 
          $clinDrugConcept4.getConceptId());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate interaction\tbasic concomitant exposure\t%s\t%s\t%s\t%s\t%s\t%s",
          currentDateStr,
          $person.getPersonId(),
          "false", "basic concomitant",
          $person.getGenderCUI(),
          ((double) ($person.getDobDateDiffInMillis(currentDate) / (1000 * 3600 * 24)) / 365),
          $dexp1.getLogInfo(),
          $dexp2.getLogInfo()
      );
      System.out.println(s);
end

// Leaf node for topical salicylates
rule "WARFARIN - SALICYLATES -- TOPICAL SALICYLATE"
    when
      $rhs : RHSState(stateName == "basic concomitant exposure of warfarin and a salicylate" && state == "yes", $person : person)

      $clinDrugConcept1 : ConceptSetItem(csName == "Salicylates Ingredients")
      $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept2 : ConceptSetItem(csName == "Topical Non-acetylated Salicylates")
      $dexp1 : ExtendedDrugExposure(drugExposureId == $rhs.getDexp2().getDrugExposureId()
        && personId == $person.getPersonId() 
        && drugConceptId == $clinDrugConcept2.getConceptId() 
        && ingredientConceptId == $clinDrugConcept1.getConceptId())
    then
      // This creates a new "state" for any patients who are found to be on both drugs...
      // This state can be used as an input on further rules to quickly filter to only those patients who have made it through previous rules
      RHSState rhsNN = new RHSState("warfarin and a topical salicylate", "yes", $person, $rhs.getDexp1(), $rhs.getDexp2());
      insertLogical(rhsNN);
      // The output string reports simply that a potential interaction was identified through concurrent drug exposures and lists the drug IDs
      String s = String.format(
        "Warfarin - Salicylate interaction for patient: %s.\n\tClinical implication: Increased risk of bleeding unlikely.\n\tMitigating factor: Presence of topical salicylate\n\tRecommendation: No special precautions.\n\tExplanation: Anticoagulant: %s; Topical Salicylate: %s;", 
        $person.getPersonId(),
        rhsNN.getDexp1().getDrugConceptId(),
        rhsNN.getDexp2().getDrugConceptId());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate interaction\twarfarin and a topical salicylate\t%s\t%s\t%s\t%s\t%s\t%s",
          currentDateStr,
          $person.getPersonId(),
          "false", "green",
          $person.getGenderCUI(),
          ((double) ($person.getDobDateDiffInMillis(currentDate) / (1000 * 3600 * 24)) / 365),
          rhsNN.getDexp1().getLogInfo(),
          rhsNN.getDexp2().getLogInfo()
      );
      System.out.println(s);
end

// filters patients with basic concomitant exposures to those on aspirin
rule "WARFARIN - SALICYLATES -- ASPIRIN"
    when
      $rhs : RHSState(stateName == "basic concomitant exposure of warfarin and a salicylate" && state == "yes", $person : person)

      $clinDrugConcept1 : ConceptSetItem(csName == "Aspirins Ingredients")
      $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept2 : ConceptSetItem(csName == "Aspirins")
      $dexp1 : ExtendedDrugExposure(drugExposureId == $rhs.getDexp2().getDrugExposureId()
        && personId == $person.getPersonId() 
        && drugConceptId == $clinDrugConcept2.getConceptId() 
        && ingredientConceptId == $clinDrugConcept1.getConceptId())
    then
      RHSState rhsNN = new RHSState("warfarin and aspirin", "yes", $person, $rhs.getDexp1(), $rhs.getDexp2());
        insertLogical(rhsNN);
      // This outputs the recommendations and rationale for the patient because this is a leaf node
      String s = String.format(
          "Matched drug exposures for the patient at the clinical drug level: Patient ID: %s; Aspirin: %s", 
          $de1.getPersonId(), 
          $clinDrugConcept2.getConceptId());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate interaction\twarfarin and aspirin\t%s\t%s\t%s\t%s\t%s\t%s",
          currentDateStr,
          $person.getPersonId(),
          "false", "",
          $person.getGenderCUI(),
          ((double) ($person.getDobDateDiffInMillis(currentDate) / (1000 * 3600 * 24)) / 365),
          rhsNN.getDexp1().getLogInfo(),
          rhsNN.getDexp2().getLogInfo()
      );
      System.out.println(s);  
end

// patient on aspirin and it is not taken for prevention of thromboembolic events
// determining this based on any history of related conditions via the concept set "History of Thromboembolic Events"
rule "WARFARIN - SALICYLATES -- THROMBOEMBOLIC EVENTS"
    when
      // This checks the state of the patients to identify only those who were identified to be on both medications of interest
      $rhs : RHSState(stateName == "warfarin and aspirin" && state == "yes", $person : person)
      // This checks to see if the patient does not have a history of thromboembolic events
      $condConcept1 : ConceptSetItem(csName == "History of Thromboembolic Events") 
      $ce1 : ConditionEra(PersonId == $person.getPersonId() && conditionConceptId == $condConcept1.getConceptId())
    then  
      // This tags a given patient as "yes for risks"... There are three potential types of risks but all of them result in this same RHS state
      RHSState rhsNN = new RHSState("warfarin and aspirin, prevention of thromboembolic events yes", "yes", $person, $rhs.getDexp1(), $rhs.getDexp2());
      insertLogical(rhsNN);
      // Nothing hugely important to output here, just an update to show how many patients were at risk due to conditions
      String s = String.format(
        "Warfarin - Salicylate interaction for patient: %s.\n\tClinical implication: Increased risk of bleeding desired.\n\tMitigating factor: (TODO)\n\tRecommendation: No special precautions.\n\tExplanation: History of thromboembolic events (condition concept id: %s)", 
        $person.getPersonId(),
        $ce1.getConditionConceptId());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate interaction\twarfarin and aspirin, prevention of thromboembolic events yes\t%s\t%s\t%s\t%s\t%s\t%s",
          currentDateStr,
          $person.getPersonId(),
          "false", "green",
          $person.getGenderCUI(),
          ((double) ($person.getDobDateDiffInMillis(currentDate) / (1000 * 3600 * 24)) / 365),
          rhsNN.getDexp1().getLogInfo(),
          rhsNN.getDexp2().getLogInfo()
      );
      System.out.println(s);
end

rule "WARFARIN - SALICYLATES -- NO THROMBOEMBOLIC EVENTS"
    when
      // This checks the state of the patients to identify only those who were identified to be on both medications of interest
      $rhs1 : RHSState(stateName == "warfarin and aspirin" && state == "yes", $person : person)
      not ($rhs2 : RHSState(stateName == "warfarin and aspirin, prevention of thromboembolic events yes" && state == "yes" && person.getPersonId() == $person.getPersonId()))
      // This checks to see if the patient does not have a history of thromboembolic events
      $condConcept1 : ConceptSetItem(csName == "History of Thromboembolic Events") 
      not ($ce1 : ConditionEra(PersonId == $person.getPersonId() && conditionConceptId == $condConcept1.getConceptId()))
    then  
      // This tags a given patient as "yes for risks"... There are three potential types of risks but all of them result in this same RHS state
      RHSState rhsNN = new RHSState("warfarin and aspirin, prevention of thromboembolic events no", "yes", $person, $rhs1.getDexp1(), $rhs1.getDexp2());
      insertLogical(rhsNN);
      // Nothing hugely important to output here, just an update to show how many patients were at risk due to conditions
      String s = String.format(
        "Warfarin - Salicylate interaction for patient: %s.\n\tClinical implication: Increased risk of bleeding probably unintentional.\n\tMitigating factor: (TODO)\n\tRecommendation: Use only if benefit outweighs risk.\n\tExplanation: No history of thromboembolic events", 
        $person.getPersonId());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate interaction\twarfarin and aspirin, prevention of thromboembolic events no\t%s\t%s\t%s\t%s\t%s\t%s",
          currentDateStr,
          $person.getPersonId(),
          "true", "red",
          $person.getGenderCUI(),
          ((double) ($person.getDobDateDiffInMillis(currentDate) / (1000 * 3600 * 24)) / 365),
          rhsNN.getDexp1().getLogInfo(),
          rhsNN.getDexp2().getLogInfo()
      );
      System.out.println(s);
end

// filters patients with basic concomitant exposures to those on a non-acetylated salicylate
// This does not include Bismuth subsalicylate, as it has a different dosage threshold than the rest
// In this case, we check to see if the dosage of the salicylate is over 3g/day (= 3000 mg/day. Use MG since this is what the unit used in the drug names/quantities) and fire an alert if so
rule "WARFARIN - SALICYLATES -- HIGH DOSAGE NON-ACETYLATED"
    when
      $rhs : RHSState(stateName == "basic concomitant exposure of warfarin and a salicylate" && state == "yes", $person : person)

      $clinDrugConcept1 : ConceptSetItem(csName == "Non-acetylated Salicylates Ingredients")
      $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept2 : ConceptSetItem(csName == "Non-acetylated Salicylates")
      $dexp1 : ExtendedDrugExposure(drugExposureId == $rhs.getDexp2().getDrugExposureId()
        && personId == $person.getPersonId() 
        && drugConceptId == $clinDrugConcept2.getConceptId() 
        && ingredientConceptId == $clinDrugConcept1.getConceptId() 
        && dailyDosage >= 3000)        
    then
      RHSState rhsNN = new RHSState("warfarin and a non-acetylated salicylate, high dosage salicylate yes", "yes", $person, $rhs.getDexp1(), $rhs.getDexp2());
      insertLogical(rhsNN);
      // This outputs the recommendations and rationale for the patient because this is a leaf node
      String s = String.format(
          "Warfarin - Salicylate interaction for patient: %s.\n\tClinical implication: Increased risk of bleeding possible.\n\tMitigating factor: High dosage of a non-acetylated salicylate (%s at %s mg/day, compared to a threshold value of 3000.0 mg/day).\n\tRecommendation: Assess risk and take action if necessary.\n\tExplanation: While non-acetylated salicylates do not affect platelets and would not interact with warfarin on that basis, high serum concentrations of salicylate can increase the INR.", 
          $person.getPersonId(),
          $dexp1.getDrugConceptId(),
          $dexp1.getDailyDosage());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate interaction\twarfarin and a non-acetylated salicylate, high dosage salicylate yes\t%s\t%s\t%s\t%s\t%s\t%s",
          currentDateStr,
          $person.getPersonId(),
          "true", "yellow",
          $person.getGenderCUI(),
          ((double) ($person.getDobDateDiffInMillis(currentDate) / (1000 * 3600 * 24)) / 365),
          rhsNN.getDexp1().getLogInfo(),
          rhsNN.getDexp2().getLogInfo()
      );
      System.out.println(s);
end

rule "WARFARIN - SALICYLATES -- NOT HIGH DOSAGE NON-ACETYLATED"
    when
      $rhs : RHSState(stateName == "basic concomitant exposure of warfarin and a salicylate" && state == "yes", $person : person)

      $clinDrugConcept1 : ConceptSetItem(csName == "Non-acetylated Salicylates Ingredients")
      $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept2 : ConceptSetItem(csName == "Non-acetylated Salicylates")
      $dexp1 : ExtendedDrugExposure(drugExposureId == $rhs.getDexp2().getDrugExposureId()
        && personId == $person.getPersonId() 
        && drugConceptId == $clinDrugConcept2.getConceptId() 
        && ingredientConceptId == $clinDrugConcept1.getConceptId() 
        && dailyDosage < 3000)        
    then
      RHSState rhsNN = new RHSState("warfarin and a non-acetylated salicylate, high dosage salicylate no", "yes", $person, $rhs.getDexp1(), $rhs.getDexp2());
        insertLogical(rhsNN);
      // This outputs the recommendations and rationale for the patient because this is a leaf node
      String s = String.format(
          "Warfarin - Salicylate interaction for patient: %s.\n\tClinical implication: Increased risk of bleeding unlikely.\n\tMitigating factor: Not high dosage of a non-acetylated salicylate (%s at %s mg/day, compared to a threshold value of 3000.0 mg/day).\n\tRecommendation: No special precautions.\n\tExplanation: While non-acetylated salicylates do not affect platelets and would not interact with warfarin on that basis, high serum concentrations of salicylate can increase the INR.", 
          $person.getPersonId(),
          $dexp1.getDrugConceptId(),
          $dexp1.getDailyDosage());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate interaction\twarfarin and a non-acetylated salicylate, high dosage salicylate no\t%s\t%s\t%s\t%s\t%s\t%s",
          currentDateStr,
          $person.getPersonId(),
          "false", "green",
          $person.getGenderCUI(),
          ((double) ($person.getDobDateDiffInMillis(currentDate) / (1000 * 3600 * 24)) / 365),
          rhsNN.getDexp1().getLogInfo(),
          rhsNN.getDexp2().getLogInfo()
      );
      System.out.println(s);
end

// NOTE previous rule file had "Bismuth Subsalicylate" rule - not in either old or new diagrams. Still include?
// filters patients with basic concomitant exposures to those taking bismuth subsalicylate
// This is a non-acetylated salicylate, so we need to check if its dosage equivalent is equal to 3000 mg/day Aspirin
// Unlike the other salicylates, bismuth subsalicylate is does not have equivalent dosage to aspirin, therefore a conversion must be made
// Bismuth Salicylate is 2.57g / 1 g of Aspirin
rule "WARFARIN - SALICYLATES -- HIGH DOSAGE BISMUTH SUBSALICYLATE"
    when
      $rhs : RHSState(stateName == "basic concomitant exposure of warfarin and a salicylate" && state == "yes", $person : person)

      $clinDrugConcept1 : ConceptSetItem(csName == "Bismuth Subsalicylates Ingredients")
      $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept2 : ConceptSetItem(csName == "Bismuth Subsalicylates")
      $dexp1 : ExtendedDrugExposure(drugExposureId == $rhs.getDexp2().getDrugExposureId()
        && personId == $person.getPersonId() 
        && drugConceptId == $clinDrugConcept2.getConceptId() 
        && ingredientConceptId == $clinDrugConcept1.getConceptId() 
        && dailyDosage >= 7710)        
    then
      RHSState rhsNN = new RHSState("warfarin and bismuth subsalicylate, high dosage salicylate yes", "yes", $person, $rhs.getDexp1(), $rhs.getDexp2());
      insertLogical(rhsNN);
      // This outputs the recommendations and rationale for the patient because this is a leaf node
      String s = String.format(
          "Warfarin - Salicylate interaction for patient: %s.\n\tClinical implication: Increased risk of bleeding.\n\tMitigating factor: High dosage of Bismuth Subsalicylate (%s at %s mg/day, compared to a threshold value of 7710.0 mg/day).\n\tRecommendation: Use only if benefit outweighs risk.\n\tExplanation: WWhile non-acetylated salicylates do not affect platelets and would not interact with warfarin on that basis, high serum concentrations of salicylate can increase the INR.", 
          $person.getPersonId(),
          $dexp1.getDrugConceptId(),
          $dexp1.getDailyDosage());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate interaction\twarfarin and bismuth subsalicylate, high dosage salicylate yes\t%s\t%s\t%s\t%s\t%s\t%s",
          currentDateStr,
          $person.getPersonId(),
          "true", "red",
          $person.getGenderCUI(),
          ((double) ($person.getDobDateDiffInMillis(currentDate) / (1000 * 3600 * 24)) / 365),
          rhsNN.getDexp1().getLogInfo(),
          rhsNN.getDexp2().getLogInfo()
      );
      System.out.println(s);
end

rule "WARFARIN - SALICYLATES -- NOT HIGH DOSAGE BISMUTH SUBSALICYLATE"
    when
      $rhs : RHSState(stateName == "basic concomitant exposure of warfarin and a salicylate" && state == "yes", $person : person)

      $clinDrugConcept1 : ConceptSetItem(csName == "Bismuth Subsalicylates Ingredients")
      $de1 : DrugEra(personId == $person.getPersonId() && drugConceptId == $clinDrugConcept1.getConceptId())
      $clinDrugConcept2 : ConceptSetItem(csName == "Bismuth Subsalicylates")
      $dexp1 : ExtendedDrugExposure(drugExposureId == $rhs.getDexp2().getDrugExposureId()
        && personId == $person.getPersonId() 
        && drugConceptId == $clinDrugConcept2.getConceptId() 
        && ingredientConceptId == $clinDrugConcept1.getConceptId() 
        && dailyDosage < 7710)        
    then
      RHSState rhsNN = new RHSState("warfarin and bismuth subsalicylate, high dosage salicylate no", "yes", $person, $rhs.getDexp1(), $rhs.getDexp2());
        insertLogical(rhsNN);
      // This outputs the recommendations and rationale for the patient because this is a leaf node
      String s = String.format(
          "Warfarin - Salicylate interaction for patient: %s.\n\tClinical implication: Increased risk of bleeding unlikely.\n\tMitigating factor: Not high dosage of Bismuth Subsalicylate (%s at %s mg/day, compared to a threshold value of 7710.0 mg/day).\n\tRecommendation: No special precautions\n\tExplanation: WWhile non-acetylated salicylates do not affect platelets and would not interact with warfarin on that basis, high serum concentrations of salicylate can increase the INR.", 
          $person.getPersonId(),
          $dexp1.getDrugConceptId(),
          $dexp1.getDailyDosage());
      System.out.println(s);
      s = String.format("DATA\t%s\t%s\tWarfarin - Salicylate interaction\twarfarin and bismuth subsalicylate, high dosage salicylate no\t%s\t%s\t%s\t%s\t%s\t%s",
          currentDateStr,
          $person.getPersonId(),
          "false", "green",
          $person.getGenderCUI(),
          ((double) ($person.getDobDateDiffInMillis(currentDate) / (1000 * 3600 * 24)) / 365),
          rhsNN.getDexp1().getLogInfo(),
          rhsNN.getDexp2().getLogInfo()
      );
      System.out.println(s);
end
